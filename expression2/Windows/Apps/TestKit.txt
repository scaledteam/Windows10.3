@name TestKit
#Windows 10.3 update 7

if(appStart("APITest",vec2(300,150),ROM["System",table]["Theme",vector]*0.9,44)){ #Cost 5 egpID.
    if(appCreate()){print("Now you open APITest! If you want see samples of API - welcome.")}
    if(romCreate()){romS("WriteTest","Success!")}
    if(appChoiceMenu(1,vec2(-150,-61),ROM["System",table]["Theme",vector]*0.5,"Activity",array("Buttons","Arrays","Texts","Slider","Activity1","Activities"))){print(appS("Activity"))} #Cost 3 egpID.
    if(activityManager(4)){switch(appS("Activity")){
        case "Buttons",
            if(activityGraphics()){
                appBox(4,vec2(150-25,50),vec2(40,40),vec(128)) #Cost 1 egpID.
                appWhiteText(5,4,"Gray",vec2(0,-12),8,1) #Cost 1 egpID.
                appBlackText(6,4,"Box",vec2(0,4),8,1) }else{ #Cost 1 egpID.
            
            if(egpInrange(egp(4))){Screen:egpColor(egp(4),vec(200)) if(appClick()){print("You use API ",apiVersion())}}else{Screen:egpColor(egp(4),vec(128))}
            if(appButton(7,"Notify",      vec2(-25,-50),vec2(80,15),hsv2rgb( 60,1,0.9))){DialogApp="APITest DialogApp 1"} #Cost 3 egpID.
            if(appButton(10,"Action",     vec2(-25,-30),vec2(80,15),hsv2rgb(120,1,0.9))){DialogApp="APITest DialogApp 2"} #Cost 3 egpID.
            if(appButton(13,"Input",      vec2(-25,-10),vec2(80,15),hsv2rgb(180,1,0.9))){DialogApp="APITest DialogApp 3"} #Cost 3 egpID.
            if(appButton(16,"BigNotify",  vec2(-25, 10),vec2(80,15),hsv2rgb(240,1,0.9))){DialogApp="APITest DialogApp 4"} #Cost 3 egpID.
            if(appButton(19,"FieldNotify",vec2(-25, 30),vec2(80,15),hsv2rgb(300,1,0.9))){DialogApp="APITest DialogApp 5"} #Cost 3 egpID.
            if(appButton(41,"Choose",     vec2(-25, 50),vec2(80,15),hsv2rgb(360,1,0.9))){DialogApp="APITest DialogApp 6"} #Cost 3 egpID.
            if(appEditTextString(22,"StringField",vec2(55,-50),60)){print(appS("StringField"))} #Cost 3 egpID.
            if(appEditTextNumber(25,"NumberField",vec2(55,-30),60)){print(appN("NumberField"))} #Cost 3 egpID.
            if(appButtonPress(28,"Button",vec2(55,-10),vec2(60,15))){print("ButtonWork!")} #Cost 3 egpID.
            if(appButton(31,"Click",vec2(55,10),vec2(60,15))){print("ButtonWork!")} #Cost 3 egpID.
            if(appButtonToggle(34,"Switcher",vec2(55,30),vec2(60,15),array("Switch1","Switch2","Switch3","Switch4"))){print("Switcher return "+appN("Switcher"))} #Cost 3 egpID.
            if(appCheckBox(37,"CheckBox","Check",vec2(25,45))){if(appN("CheckBox")){print("Checked!")}else{print("Not checked!")}} #Cost 4 egpID.
        } break
        case "Arrays",
            if(activityCreate()){
                local Array = array()
                for(I=1,15){Array[I,string]="String"+I}
                appR("ArrayFieldIn",Array)
            }
            
            appArrayField(4,"ArrayField",vec2(-15,4),vec2(110,130),appR("ArrayFieldIn")) #Cost 5 egpID
            appArrayFieldUseCTM(9,"ArrayFieldUseCTM",vec2(95,4),vec2(110,130),appR("ArrayFieldIn"),array("Action1","Action2")) #Cost 6 egpID
            
            switch(appN("ArrayFieldUseCTMAction")){
                case 1, print("Action1 "+appR("ArrayFieldIn")[appN("ArrayFieldUseCTM"),string]) break #Action1
                case 2, print("Action2 "+appR("ArrayFieldIn")[appN("ArrayFieldUseCTM"),string]) break #Action2
            } if(appN("ArrayFieldUseCTMAction")){appN("ArrayFieldUseCTMAction",0)}
        break
        case "Texts",
            if(activityGraphics()){
                appUniText(4,("This is a long text function (appUniText). If you have long text - use shortArray(230/textSizeX(10)). You can use this for arrays and strings. "+ #Cost 1 EGPID
                "If you want to make simple text, you can use appWhiteText, appBlackText and appUniText. Uni - universal color."):shortArray(230/textSizeX(8)),vec2(-150+85,-60),8) 
            }
            
            #Actions of Texts page. Here place is empty, because this page have static text, and do nothink.
        break
        case "Slider",
            if(activityGraphics()){
                appUniText(4,"Static",vec2(-30,-60),10,1)
                appUniText(5,"Dynamic",vec2(60,-60),10,1)
                appBox(6,vec2(-30,-20),vec2(40),vec(appN("Slider1")))
                appBox(7,vec2( 60,-20),vec2(40),vec(appN("Slider2")))
            }
            if(appSliderStatic(  8,"Slider1",vec2(-60,10),50,0,255)){Screen:egpColor(egp(6),vec(appN("Slider1")))}
            if(appSliderDynamic(11,"Slider2",vec2( 30,10),50,0,255)){Screen:egpColor(egp(7),vec(appN("Slider2")))}
            if(appClick()){
                if(egpInrange(egp(6))){appN("Slider1",0) Screen:egpColor(egp(6),vec())}
                if(egpInrange(egp(7))){appN("Slider2",0) Screen:egpColor(egp(7),vec())}
            }
        break
        case "Activity1",
            if(activityGraphics()){
                appUniText(4,"This is Activity1",vec2(40,-20),10,1)
            }else{
            if(appButton(5,"Go to Activity2",vec2(40,0),vec2(100,15))){appS("Activity","Activity2")} #Cost 3 egpID.
            
        } break
        case "Activity2",
            if(activityGraphics()){
                appUniText(4,array("This is Activity2"):add("This is a new method - activities. Try this method on programming!":shortArray(230/textSizeX(10))),vec2(-60,-55))
                appN("CheckTested",romN("Tested"))
            }else{
            if(appButton(5,"Go back",vec2(-25,30),vec2(80,15))){appS("Activity","Activity1")} #Cost 3 egpID.
            if(appCheckBox(8,"CheckTested","I've tested new OS!",vec2(85-150,45))){romN("Tested",appN("CheckTested"))} #Cost 4 egpID.
            
        } break
        case "Activities",
            if(activityCreate()){
                print("activityCreate() of main activity manager")
            }
            appChoiceMenu(4,vec2(-70,-61),ROM["System",table]["Theme",vector]*0.5,"ActivityManager2",array("Activity1","Activity2","Activity3"))
            if(activityManager("ActivityManager2",7,10)){switch(appS("ActivityManager2")){
                case "Activity1",
                if(activityCreate("ActivityManager2")){
                    print("activityCreate() of second activity manager. Now you launch Activity1")
                }
                if(activityGraphics("ActivityManager2")){
                    appBox(7,vec2(80,0),vec2(50),vec(150,50,50))
                }
                break
                case "Activity2",
                if(activityCreate("ActivityManager2")){
                    print("activityCreate() of second activity manager. Now you launch Activity2")
                }
                if(activityGraphics("ActivityManager2")){
                    appUniText(7,"Opcounter: "+appN("Opcounter"),vec2(10,-50))
                }
                Screen:egpSetText(egp(7),"Opcounter: "+appN("Opcounter"))
                break
                case "Activity3",
                if(activityCreate("ActivityManager2")){
                    print("activityCreate() of second activity manager. Now you launch Activity2")
                }
                if(activityGraphics("ActivityManager2")){
                    appUniText(7,"Ops use: "+ops(),vec2(10,-50))
                }
                Screen:egpSetText(egp(7),"Ops use: "+ops())
                break
            }}
        break
        
    }}
    if(DialogApp){
        if(dialogApp("APITest DialogApp 1","Notify",10,"I'm a dialog app!")){print("Action after press OK button")}
        if(dialogApp("APITest DialogApp 2","Action",10,"I'm a dialog app!")){print("Action of dialog app!")}
        if(dialogApp("APITest DialogApp 3","Input", 10,"I'm a dialog app!")){print(glbS("DialogApp","OutText"))}
           dialogApp("APITest DialogApp 4","BigNotify",  10,"I'm a big dialog app! This is a big notification! If you have long message, you can use this method! If you want use array, you need concat(\"\n\") method or something same.")
           dialogApp("APITest DialogApp 5","FieldNotify",40,"I'm a big dialog app! This is a big notification! If you have long message, you can use this method! If you want use array, you need concat(\"\n\") method or something same.")  }
        if(dialogApp("APITest DialogApp 6","Choose",40,array("Choice1","Choice2","Choice3","Choice4"):concat("\n"))){print(glbS("DialogApp","OutText"))}
    
appEnd()}



if(appStart("PerfTest",vec2(200,150),vec(180,80,180),10)){
    if(appGraphics()){
        appWhiteText(1,"Test your PC!",vec2(-90,-60))
        appWhiteText(2,"Ops: "+ops(),vec2(-90,-50))
        appWhiteText(3,array(
            "Maximum of",
            "    EGPIDs: "+egpMaxObjects(),
            "    Umsg/sec: "+egpMaxUmsgPerSecond(),
            "Quota",
            "    Hard: "+hardQuota(),
            "    Soft: "+softQuota(),
            "    Max: "+maxquota(),
            "    Min: "+minquota()),vec2(-90,-30))
    }else{
    appSliderStatic(4,"Perf",vec2(-90,-35),100,0,110)
    appButtonToggle(7,"Start",vec2(70,60),vec2(40,15),array("Start","Stop"))
    Screen:egpSetText(egp(2),"Ops: "+ops())
    if(appN("Start")){
        CPUTestPerf=appN("Perf")
        if(CPUTestPerf>100){ 
            CPUTestSpeed=0 while(perf()){CPUTestSpeed++}
            Screen:egpSetText(egp(1),"Speed: "+CPUTestSpeed+" Op/Tick")
        }else{
            CPUTestSpeed=0 while(perf(CPUTestPerf)){CPUTestSpeed++}
            Screen:egpSetText(egp(1),"Speed: "+CPUTestSpeed+" Op/Tick")
        }
    }
    
} appEnd()}



if(appStart("TableCMD",vec2(300,150),vec(90,90,180),25)){
    if(appCreate()){
        romCreate() appV2("Size",vec2(300,150))
        appN("LevelL",0) appN("LevelR",0)
         local Table=table()
            #Table["ROM",table]=ROM["Apps",table]
            Table["ROM",table]=ROM["Apps",table]
            Table["RAM",table]=App
            Table["System",table]=ROM["System",table]
            Table["My files",table]=ROM["Apps",table][ThisApp,table]
        appT("FieldL"+appN("LvlL"),Table) appT("FieldR"+appN("LvlR"),Table)
        
        appR("FieldLShow",appT("FieldL"+appN("LvlL")):tableCMDtoStringArray(appV2("Size"):x()/2/textSizeX(10)))
        appR("FieldRShow",appT("FieldR"+appN("LvlR")):tableCMDtoStringArray(appV2("Size"):x()/2/textSizeX(10)))
    }else{
    if(appGraphics()){
        local Size=appV2("Size")
        Screen:egpSetText(egp(-1),"TableCommander")
        Screen:egpLineStrip(egp(13),
            vec2(Size:x()/2-Size:x()*1/7,Size:y()/2-14),
            vec2(Size:x()/2-Size:x()*1/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*2/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*2/7,Size:y()/2-14),
            vec2(Size:x()/2-Size:x()*3/7,Size:y()/2-14),
            vec2(Size:x()/2-Size:x()*3/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*4/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*4/7,Size:y()/2-14),
            vec2(Size:x()/2-Size:x()*5/7,Size:y()/2-14),
            vec2(Size:x()/2-Size:x()*5/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*6/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*6/7,Size:y()/2-14),
            vec2(-Size:x()/2,Size:y()/2-14),
            vec2(-Size:x()/2,Size:y()/2),
            vec2(Size:x()/2,Size:y()/2),
            vec2(Size:x()/2,Size:y()/2-14),
            vec2(-Size:x()/2,Size:y()/2-14)
        )
        Screen:egpColor(egp(13),vec(180,80,180)*0.7) Screen:egpSize(egp(13),2) Screen:egpParent(egp(13),egp())
        appBox(14,vec2(),vec2(Size:x()/7,15),vec(180,80,180)*0.7) Screen:egpAlpha(egp(14),0)
        appN("Pick",0)
        for(I=0,6){
            appWhiteText(15+I,select(I+1,"Add","Edit","Pick","Move","Copy","Delete","Rename"),vec2(Size:x()*(I+0.5)/7-Size:x()/2,Size:y()/2-15*0.75),7.5,1)
        }
    }else{
    
    appChat()
    
    local EGP = egp()
    
    if(appSize(22,vec2(200,100),vec2(300,150))){
        local Size=appV2("Size")
        Screen:egpRemove(EGP+2) Screen:egpRemove(EGP+2+2)
        Screen:egpRemove(EGP+7) Screen:egpRemove(EGP+7+2)
        appN("Pick",0)
        for(I=0,6){
            Screen:egpPos(egp(15+I),vec2(Size:x()*(I+0.5)/7-Size:x()/2,Size:y()/2-15*0.75))
        }
        
        Screen:egpLineStrip(egp(13),
            vec2(Size:x()/2-Size:x()*1/7,Size:y()/2-14),
            vec2(Size:x()/2-Size:x()*1/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*2/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*2/7,Size:y()/2-14),
            vec2(Size:x()/2-Size:x()*3/7,Size:y()/2-14),
            vec2(Size:x()/2-Size:x()*3/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*4/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*4/7,Size:y()/2-14),
            vec2(Size:x()/2-Size:x()*5/7,Size:y()/2-14),
            vec2(Size:x()/2-Size:x()*5/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*6/7,Size:y()/2),
            vec2(Size:x()/2-Size:x()*6/7,Size:y()/2-14),
            vec2(-Size:x()/2,Size:y()/2-14),
            vec2(-Size:x()/2,Size:y()/2),
            vec2(Size:x()/2,Size:y()/2),
            vec2(Size:x()/2,Size:y()/2-14),
            vec2(-Size:x()/2,Size:y()/2-14)
        )
    }
    
    if(appTimer(1000)){Update="TableCMD FieldL"}
    if(appTimer(1000)){Update="TableCMD FieldR"}
    
    local Size=appV2("Size")
    
    if(Update=="TableCMD FieldL"){
        appR("FieldLShow",appT("FieldL"+appN("LvlL")):tableCMDtoStringArray(Size:x()/2/textSizeX(10)))
    }
    
    if(Update=="TableCMD FieldR"){
        appR("FieldRShow",appT("FieldR"+appN("LvlR")):tableCMDtoStringArray(Size:x()/2/textSizeX(10)))
    }
    
    appArrayFieldUse(2,"FieldL",Size*vec2(-0.25,0),Size*vec2(0.5,1)-vec2(0,30),array("<Back"):add(appR("FieldLShow")))
    
    appArrayFieldUse(7,"FieldR",Size*vec2(0.25,0),Size*vec2(0.5,1)-vec2(0,30),array("<Back"):add(appR("FieldRShow")))
    
    if(appClick()){
        if(egpInrange(EGP+2)){appS("Focus","L") Screen:egpAlpha(EGP+7+4,0)}
        if(egpInrange(EGP+7)){appS("Focus","R") Screen:egpAlpha(EGP+2+4,0)}
        
        local F=appS("Focus")
        local Lvl=appN("Lvl"+F)
        
        local Choice=appT("Field"+F+Lvl):keys()[appN("Field"+F)-1,string]
        if(!appN("Pick")&appT("Field"+F+Lvl):typeids()[Choice,string]=="t"){
            appT("Field"+F+(Lvl+1):toString(),appT("Field"+F+Lvl)[Choice,table])
            appN("Lvl"+F,Lvl+1) 
            
            appN("Field"+F,0)
            Screen:egpAlpha(EGP+2+4,0)
            Screen:egpAlpha(EGP+7+4,0)
            Update="TableCMD Field"+F
        }
        
        elseif(appN("Field"+F)==1&Lvl>0){
            appN("Lvl"+F,Lvl-1) 
            
            appN("Field"+F,0)
            Screen:egpAlpha(EGP+2+4,0)
            Screen:egpAlpha(EGP+7+4,0)
            Update="TableCMD Field"+F
        }
    }
    
    local F=appS("Focus")
    local Lvl=appN("Lvl"+F)
    local Choice=appT("Field"+F+Lvl):keys()[appN("Field"+F)-1,string]
    
    local InrangeStart=vec2(-Size:x()/2,Size:y()/2-15)
    if(egpAppInrange(InrangeStart,Size/2)){
        local ChoiceButton = clamp(appChoiceHelper(InrangeStart,Size:x()/7):x(),0,6)
        if(appClick()){switch(ChoiceButton){
            case 0, DialogApp="TableCMD Add" break
            case 1, DialogApp="TableCMD Edit" break
            case 2, 
                Screen:egpSetText(egp(17),select(!appN("Pick")+1,"Pick","Cancel"))
                appN("Pick",!appN("Pick"))
                appN("Field"+F,0)
                Screen:egpAlpha(EGP+2+4,0)
                Screen:egpAlpha(EGP+7+4,0)
            break
            case 3, 
                local F2="U" if(F=="L"){F2="R"}elseif(F=="R"){F2="L"}
                local Lvl2=appN("Lvl"+F2)
                local Target=appT("Field"+F2+Lvl2)
                switch(appT("Field"+F+Lvl):typeids()[Choice,string]){
                    case "n", Target[Choice,number] = appT("Field"+F+Lvl)[Choice,number] break
                    case "s", Target[Choice,string] = appT("Field"+F+Lvl)[Choice,string] break
                    case "v", Target[Choice,vector] = appT("Field"+F+Lvl)[Choice,vector] break
                    case "xv2", Target[Choice,vector2] = appT("Field"+F+Lvl)[Choice,vector2] break
                    case "xv4", Target[Choice,vector4] = appT("Field"+F+Lvl)[Choice,vector4] break
                    case "e", Target[Choice,entity]= appT("Field"+F+Lvl)[Choice,entity] break
                    case "a", Target[Choice,angle] = appT("Field"+F+Lvl)[Choice,angle] break
                    case "r", Target[Choice,array] = appT("Field"+F+Lvl)[Choice,array] break
                    case "t", Target[Choice,table] = appT("Field"+F+Lvl)[Choice,table] break
                    default, DialogApp="TableCMD Can't move" break
                }
                appT("Field"+F+Lvl):remove(Choice)
            break
            case 4,
                local F2="U" if(F=="L"){F2="R"}elseif(F=="R"){F2="L"}
                local Lvl2=appN("Lvl"+F2)
                local Target=appT("Field"+F2+Lvl2)
                switch(appT("Field"+F+Lvl):typeids()[Choice,string]){
                    case "n", Target[Choice,number] = appT("Field"+F+Lvl)[Choice,number] break
                    case "s", Target[Choice,string] = appT("Field"+F+Lvl)[Choice,string] break
                    case "v", Target[Choice,vector] = appT("Field"+F+Lvl)[Choice,vector] break
                    case "xv2", Target[Choice,vector2] = appT("Field"+F+Lvl)[Choice,vector2] break
                    case "xv4", Target[Choice,vector4] = appT("Field"+F+Lvl)[Choice,vector4] break
                    case "e", Target[Choice,entity]= appT("Field"+F+Lvl)[Choice,entity] break
                    case "a", Target[Choice,angle] = appT("Field"+F+Lvl)[Choice,angle] break
                    case "r", Target[Choice,array] = appT("Field"+F+Lvl)[Choice,array]:clone() break
                    case "t", Target[Choice,table] = appT("Field"+F+Lvl)[Choice,table]:clone() break
                    default, DialogApp="TableCMD Can't copy" break
                }
            break
            case 5, appT("Field"+F+Lvl):remove(Choice) Update="TableCMD FieldL" break
            case 6, DialogApp="TableCMD Rename" break
        }}
        Screen:egpBox(egp(14),InrangeStart+vec2(Size:x()/7*(ChoiceButton+0.5),7.5),vec2(Size:x()/7,15)) 
        Screen:egpAlpha(egp(14),255)
    }else{Screen:egpAlpha(egp(14),0)}
    
    if(dialogApp("TableCMD Add","Choose",60,array("number","string","vector","vector2","vector4","entity","angle","table","numberArray","stringArray"):concat("\n"))){
        appS("Type",glbS("DialogApp","OutText"))
        appN("TableCMDAdd2",1)
    }elseif(appN("TableCMDAdd2")){
        DialogApp="TableCMD Add2"
        appN("TableCMDAdd2",0)
    }
    
    if(dialogApp("TableCMD Add2","Input",10,"Enter name:")){
        appS("Name",glbS("DialogApp","OutText"))
        appN("TableCMDAdd3",1)
    }elseif(appN("TableCMDAdd3")){
        DialogApp="TableCMD Add3"
        appN("TableCMDAdd3",0)
    }
    
    if(dialogApp("TableCMD Edit","Choose",60,array("number","string","vector","vector2","vector4","entity","angle","table","numberArray","stringArray"):concat("\n"))){
        appS("Type",glbS("DialogApp","OutText"))
        appS("Name",Choice)
        appN("TableCMDEdit",1)
        if(appS("Type")=="numberArray"|appS("Type")=="stringArray"){SysText=appT("Field"+F+Lvl)[appS("Name"),array]:concat(",")}
    }elseif(appN("TableCMDEdit")){
        DialogApp="TableCMD Add3"
        appN("TableCMDEdit",0)
    }
    
    if(DialogApp){
        dialogApp("TableCMD Can't copy","Notify",10,"Can't copy value.")
        dialogApp("TableCMD Can't move","Notify",10,"Can't move value.")
        dialogApp("TableCMD Can't rename","Notify",10,"Can't rename value.")
        
        if(dialogApp("TableCMD Add3","Input",8,"Enter data: (Use ',' for explode)")){
            local Paste=appT("Field"+F+Lvl)
            local Explode=glbS("DialogApp","OutText"):explode(",")
            if(appS("Name")!=""){switch(appS("Type")){
                case "number",  Paste[appS("Name"),number]=glbS("DialogApp","OutText"):toNumber() break
                case "string",  Paste[appS("Name"),string]=glbS("DialogApp","OutText") break
                case "vector",  Paste[appS("Name"),vector]=vec(Explode[1,string]:toNumber(),Explode[2,string]:toNumber(),Explode[3,string]:toNumber()) break
                case "vector2", Paste[appS("Name"),vector2]=vec2(Explode[1,string]:toNumber(),Explode[2,string]:toNumber()) break
                case "vector4", Paste[appS("Name"),vector4]=vec4(Explode[1,string]:toNumber(),Explode[2,string]:toNumber(),Explode[3,string]:toNumber(),Explode[4,string]:toNumber()) break
                case "angle",   Paste[appS("Name"),angle]=ang(Explode[1,string]:toNumber(),Explode[2,string]:toNumber(),Explode[3,string]:toNumber()) break
                case "entity",  Paste[appS("Name"),entity]=entity(glbS("DialogApp","OutText"):toNumber()) break
                case "table",   Paste[appS("Name"),table]=table() break
                case "numberArray", foreach(I,N:string=Explode){Explode[I,number]=N:toNumber()} Paste[appS("Name"),array]=Explode break
                case "stringArray", Paste[appS("Name"),array]=Explode break
            }}
            appT("Field"+F+Lvl,Paste)
            Update="TableCMD Field"+F
        }
        
        if(dialogApp("TableCMD Rename","Input",10,"Enter new name")){
            local Target=appT("Field"+F+Lvl)
            local Paste=appT("Field"+F+Lvl)
            local Name=glbS("DialogApp","OutText")
            if(Name!=""){switch(appT("Field"+F+Lvl):typeids()[Choice,string]){
                case "n", Target[Name,number] = Target[Choice,number] break
                case "s", Target[Name,string] = Target[Choice,string] break
                case "v", Target[Name,vector] = Target[Choice,vector] break
                case "xv2", Target[Name,vector2] = Target[Choice,vector2] break
                case "xv4", Target[Name,vector4] = Target[Choice,vector4] break
                case "e", Target[Name,entity]= Target[Choice,entity] break
                case "a", Target[Name,angle] = Target[Choice,angle] break
                case "r", Target[Name,array] = Target[Choice,array]:clone() break
                case "t", Target[Name,table] = Target[Choice,table]:clone() break
                default, DialogApp="TableCMD Can't rename" break
            }}
            appT("Field"+F+Lvl,Paste)
            Update="TableCMD Field"+F
            Target:remove(Choice)
        }
    }
    
}} appEnd()}
